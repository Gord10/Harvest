title: HelloThere
tags:
---
Stranger: Hello, there.
Stranger: Would you want to sit?
-> Player: Yes.
	<<FadeOut GameManager>>
	<<wait 1>>
	<<LoadScene GameManager ChatWithHarvester>>
-> Player: No, thanks.
===
title: HarvestSeason
tags:
---
Stranger: What a lovely fall day, isn't it?
Stranger: Or should I have said "harvest" day?
-> Yes.
	<<jump MyFavouriteSeason>>
-> Why "harvest" day?
	<<jump HarvestSeasonExplain>>
===
title: HarvestSeasonExplain
tags:
---
Stranger: In Old English, "harvest" meant "fall season".
Stranger: You know, they were harvesting the crops at that time of year.
Stranger: And this is poetic.
<<jump MyFavouriteSeason>>
===
title: MyFavouriteSeason
---
Stranger: And it's my favourite season.
Stranger: When things pass away to reach a level of existence above here.
-> So is mine.
	<<set $favSeason to "harvest">>
-> I like the peace of winter.
	<<set $favSeason to "winter">>
-> I am a summer person.
	<<set $favSeason to "summer">>
-> I like the joy that spring brings.
	<<set $favSeason to "spring">>
<<jump IKnowItsYourFavouriteSeason>>
===
title: IKnowItsYourFavouriteSeason
---
Stranger: Oh, I know {$favSeason} is your favourite season.
Stranger: Actually, I know *everything* about you.
-> Oh?
-> You do?
<<jump IHaveBeenWatchingYou>>
===
title: IHaveBeenWatchingYou
---
Stranger: I have been watching you.
Stranger: Not only when you had been wandering in this park.
Stranger: Not after you gained fame as an award-winning journalist.
Stranger: But since you were born.
-> Who are you?
	Stranger: I am not a person.
	Stranger: "Who" is not the right question.
	<<jump HarvesterReveal>>
-> What are you?
	Stranger: "What" is the correct question, for I'm no human like you.
	<<jump HarvesterReveal>>
===
title: HarvesterReveal
---
<<TurnIntoAngel Harvester>>
Harvester: Call me "Harvester".
===